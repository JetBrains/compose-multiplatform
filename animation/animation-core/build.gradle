/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import androidx.build.AndroidXUiPlugin
import androidx.build.LibraryGroups
import androidx.build.LibraryType
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static androidx.build.dependencies.DependenciesKt.*

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXUiPlugin")
}

AndroidXUiPlugin.applyAndConfigureKotlinPlugin(project)

dependencies {
    kotlinPlugin(project(":compose:compiler:compiler"))

    if (!AndroidXUiPlugin.isMultiplatformEnabled(project)) {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block below
         */

        api("androidx.annotation:annotation:1.1.0")

        implementation(project(":compose:runtime:runtime"))
        implementation(project(":compose:ui:ui-unit"))
        implementation(project(":compose:ui:ui-util"))
        implementation(KOTLIN_STDLIB)
        api(KOTLIN_COROUTINES_CORE)

        testImplementation(ANDROIDX_TEST_RULES)
        testImplementation(ANDROIDX_TEST_RUNNER)
        testImplementation(JUNIT)
        testImplementation(TRUTH)
        testImplementation(KOTLIN_COROUTINES_CORE)

        androidTestImplementation(ANDROIDX_TEST_RULES)
        androidTestImplementation(ANDROIDX_TEST_RUNNER)
        androidTestImplementation(ANDROIDX_TEST_CORE)
        androidTestImplementation(JUNIT)
        androidTestImplementation(project(":compose:animation:animation"))
        androidTestImplementation(project(":compose:ui:ui-test-junit4"))
        androidTestImplementation(project(":compose:test-utils"))

        lintPublish project(":compose:animation:animation-core-lint")
    }
}

if (AndroidXUiPlugin.isMultiplatformEnabled(project)) {
    kotlin {
        android()
        jvm("desktop")

        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block above
         */
        sourceSets {

            jvmMain {
                dependencies {
                    implementation(KOTLIN_STDLIB)
                    api(KOTLIN_COROUTINES_CORE)
                }
            }

            commonMain.dependencies {
                implementation(project(":compose:runtime:runtime"))
                implementation(project(":compose:ui:ui-unit"))
                implementation(project(":compose:ui:ui-util"))
                implementation(KOTLIN_STDLIB_COMMON)
                api(KOTLIN_COROUTINES_CORE)
            }

            androidMain {
                dependencies {
                    api("androidx.annotation:annotation:1.1.0")
                    implementation(KOTLIN_STDLIB)
                }
                dependsOn(jvmMain)
            }
            desktopMain {
                dependencies {
                    implementation(KOTLIN_STDLIB)
                }
                dependsOn(jvmMain)
            }

            androidTest.dependencies {
                implementation(ANDROIDX_TEST_RULES)
                implementation(ANDROIDX_TEST_RUNNER)
                implementation(JUNIT)
                implementation(TRUTH)
                implementation(KOTLIN_COROUTINES_CORE)
            }

            androidAndroidTest.dependencies {
                implementation(ANDROIDX_TEST_RULES)
                implementation(ANDROIDX_TEST_RUNNER)
                implementation(ANDROIDX_TEST_CORE)
                implementation(JUNIT)
                implementation(project(":compose:animation:animation"))
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(project(":compose:test-utils"))
            }
        }
    }
}

androidx {
    name = "Compose Animation Core"
    type = LibraryType.PUBLISHED_LIBRARY
    mavenGroup = LibraryGroups.Compose.ANIMATION
    inceptionYear = "2019"
    description = "Animation engine and animation primitives that are the building blocks of the Compose animation library"
    legacyDisableKotlinStrictApiMode = true
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += [
                "-Xuse-experimental=kotlin.Experimental",
        ]
    }
}
