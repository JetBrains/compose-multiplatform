/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import androidx.build.AndroidXUiPlugin
import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.Publish
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static androidx.build.dependencies.DependenciesKt.*

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXUiPlugin")
}

AndroidXUiPlugin.applyAndConfigureKotlinPlugin(project)

dependencies {
    kotlinPlugin project(":compose:compiler:compiler")

    if(!AndroidXUiPlugin.isMultiplatformEnabled(project)) {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block below
         */

        api "androidx.annotation:annotation:1.1.0"
        api project(":compose:animation:animation-core")
        api project(":compose:foundation:foundation-layout")
        api project(":compose:runtime:runtime")
        api project(":compose:ui:ui")
        api project(":compose:ui:ui-geometry")

        implementation project(":compose:ui:ui-util")
        implementation(KOTLIN_STDLIB_COMMON)

        testImplementation(ANDROIDX_TEST_RULES)
        testImplementation(ANDROIDX_TEST_RUNNER)
        testImplementation(JUNIT)

        androidTestImplementation project(":compose:foundation:foundation")
        androidTestImplementation project(":compose:ui:ui-test-junit4")
        androidTestImplementation(ANDROIDX_TEST_RULES)
        androidTestImplementation(ANDROIDX_TEST_RUNNER)
        androidTestImplementation(JUNIT)
    }
}

if(AndroidXUiPlugin.isMultiplatformEnabled(project)) {
    kotlin {
        android()
        jvm("desktop")

        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block above
         */
        sourceSets {
            commonMain.dependencies {
                implementation(KOTLIN_STDLIB_COMMON)

                api project(":compose:animation:animation-core")
                api project(":compose:foundation:foundation-layout")
                api project(":compose:runtime:runtime")
                api project(":compose:ui:ui")
                api project(":compose:ui:ui-geometry")

                implementation project(":compose:ui:ui-util")
            }

            androidMain.dependencies {
                api "androidx.annotation:annotation:1.1.0"
            }

            androidTest.dependencies {
                implementation(ANDROIDX_TEST_RULES)
                implementation(ANDROIDX_TEST_RUNNER)
                implementation(JUNIT)
            }

            androidAndroidTest.dependencies {
                implementation(ANDROIDX_TEST_RULES)
                implementation(ANDROIDX_TEST_RUNNER)
                implementation(JUNIT)
                implementation project(":compose:foundation:foundation")
                implementation project(":compose:ui:ui-test-junit4")
            }
        }
    }
}

androidx {
    name = "Compose Animation"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenGroup = LibraryGroups.Compose.ANIMATION
    inceptionYear = "2019"
    description = "Compose animation library"
    legacyDisableKotlinStrictApiMode = true
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        useIR = true
    }
}
