/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.AndroidXComposePlugin
import androidx.build.Publish

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

AndroidXComposePlugin.applyAndConfigureKotlinPlugin(project)

dependencies {
    kotlinPlugin(projectOrArtifact(":compose:compiler:compiler"))

    if(!AndroidXComposePlugin.isMultiplatformEnabled(project)) {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block below
         */

        api("androidx.activity:activity:1.2.0")
        api(projectOrArtifact(":compose:ui:ui-test-junit4"))
        api(project(":test-screenshot"))

        implementation(libs.kotlinStdlibCommon)
        implementation(projectOrArtifact(":compose:runtime:runtime"))
        implementation(projectOrArtifact(":compose:ui:ui-unit"))
        implementation(projectOrArtifact(":compose:ui:ui-graphics"))
        implementation(projectOrArtifact(":activity:activity-compose"))
        implementation(libs.testCore)
        implementation(libs.testRules)

        // This has stub APIs for access to legacy Android APIs, so we don't want
        // any dependency on this module.
        compileOnly(projectOrArtifact(":compose:ui:ui-android-stubs"))

        testImplementation(libs.truth)

        androidTestImplementation(libs.truth)
        androidTestImplementation(projectOrArtifact(":compose:material:material"))
    }
}

if (AndroidXComposePlugin.isMultiplatformEnabled(project)) {
    kotlin {
        android()
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block above
         */
        sourceSets {
            commonMain.dependencies {
                implementation(libs.kotlinStdlibCommon)
                implementation(projectOrArtifact(":compose:runtime:runtime"))
                implementation(projectOrArtifact(":compose:ui:ui-unit"))
                implementation(projectOrArtifact(":compose:ui:ui-graphics"))
                implementation(projectOrArtifact(":compose:ui:ui-test-junit4"))
            }

            androidMain.dependencies {
                api("androidx.activity:activity:1.2.0")
                implementation project(":activity:activity-compose")
                api(projectOrArtifact(":compose:ui:ui-test-junit4"))
                api(project(":test-screenshot"))
                // This has stub APIs for access to legacy Android APIs, so we don't want
                // any dependency on this module.
                compileOnly(projectOrArtifact(":compose:ui:ui-android-stubs"))
                implementation(libs.testCore)
                implementation(libs.testRules)
            }

            test.dependencies {
                implementation(libs.truth)
            }

            androidAndroidTest.dependencies {
                implementation(libs.truth)
                implementation(projectOrArtifact(":compose:material:material"))
            }
        }
    }
}

androidx {
    name = "Compose Internal Test Utils"
    publish = Publish.NONE
    inceptionYear = "2020"
    description = "Compose internal test utils."
}
