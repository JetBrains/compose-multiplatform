import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
import org.jetbrains.compose.desktop.application.dsl.TargetFormat

plugins {
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.plugin.compose"
    id "org.jetbrains.compose"
    id "org.jetbrains.kotlin.plugin.serialization"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:1.9.0"
    implementation compose.desktop.currentOs
    implementation compose.material3
}

compose.desktop {
    application {
        mainClass = "Main"
        nativeDistributions {
            windows {
                console = true
            }
            targetFormats(TargetFormat.Dmg, TargetFormat.Msi, TargetFormat.Deb)
        }

        def projectPath = project.projectDir.absolutePath
        if (DefaultNativePlatform.currentOperatingSystem.isWindows()) {
            projectPath = projectPath.replace("\\", "\\\\")
        }
        args(projectPath)

        buildTypes.release.proguard {
            configurationFiles.from("rules.pro")
        }
    }
}