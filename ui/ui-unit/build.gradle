/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import androidx.build.AndroidXUiPlugin
import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.Publish
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static androidx.build.dependencies.DependenciesKt.*

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXUiPlugin")
}

AndroidXUiPlugin.applyAndConfigureKotlinPlugin(project)

if(!AndroidXUiPlugin.isMultiplatformEnabled(project)) {
    dependencies {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block below
         */

        api project(":compose:ui:ui-geometry")
        api "androidx.annotation:annotation:1.1.0"

        implementation(KOTLIN_STDLIB)
        implementation project(":compose:runtime:runtime")
        implementation project(":compose:ui:ui-util")

        testImplementation(JUNIT)
        testImplementation(TRUTH)

        androidTestImplementation(ANDROIDX_TEST_RULES)
        androidTestImplementation(ANDROIDX_TEST_RUNNER)
        androidTestImplementation(ANDROIDX_TEST_EXT_JUNIT)
        androidTestImplementation(ESPRESSO_CORE)
        androidTestImplementation(TRUTH)
        androidTestImplementation(KOTLIN_TEST)
    }
}

if(AndroidXUiPlugin.isMultiplatformEnabled(project)) {
    kotlin {
        android()
        jvm("desktop")

        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block above
         */
        sourceSets {
            commonMain.dependencies {
                implementation(KOTLIN_STDLIB_COMMON)
                api project(":compose:ui:ui-geometry")

                implementation project(":compose:runtime:runtime")
                implementation project(":compose:ui:ui-util")
            }
            jvmMain.dependencies {
                implementation(KOTLIN_STDLIB)
            }
            androidMain.dependencies {
                api "androidx.annotation:annotation:1.1.0"
            }

            commonTest.dependencies {
                implementation kotlin("test-junit")
            }

            androidTest.dependencies {
                implementation(TRUTH)
            }
            androidAndroidTest.dependencies {
                implementation(ANDROIDX_TEST_RULES)
                implementation(ANDROIDX_TEST_RUNNER)
                implementation(ANDROIDX_TEST_EXT_JUNIT)
                implementation(ESPRESSO_CORE)
            }
        }
    }
}

androidx {
    name = "Compose Unit"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenVersion = LibraryVersions.COMPOSE
    mavenGroup = LibraryGroups.Compose.UI
    inceptionYear = "2020"
    description = "Compose classes for simple units"
    legacyDisableKotlinStrictApiMode = true
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += ["-XXLanguage:+InlineClasses"]
        useIR = true
    }
}
