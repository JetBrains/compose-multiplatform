/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.Publish
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static androidx.build.dependencies.DependenciesKt.*

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXUiPlugin")
    id("kotlin-multiplatform")
}

kotlin {
    android()
    jvm("desktop")

    sourceSets {
        commonMain.dependencies {
            implementation(KOTLIN_STDLIB_COMMON)

            api project(":compose:ui:ui-graphics")
            api project(":compose:ui:ui-unit")

            implementation project(":compose:runtime:runtime")
            implementation project(":compose:runtime:runtime-saved-instance-state")
            implementation project(":compose:ui:ui-util")
        }

        jvmMain.dependencies {
            implementation(KOTLIN_STDLIB)
        }

        desktopMain.dependencies {
            implementation(SKIKO)
        }

        desktopMain.dependsOn jvmMain

        androidMain.dependencies {
            api "androidx.annotation:annotation:1.1.0"
            implementation "androidx.collection:collection:1.1.0"
            implementation "androidx.core:core:1.5.0-alpha01"
            implementation project(":compose:ui:ui-text-android")
        }

        androidTest.dependencies {
            implementation project(":compose:ui:ui-test-font")
            implementation(ANDROIDX_TEST_RULES)
            implementation(ANDROIDX_TEST_RUNNER)
            implementation(JUNIT)
            implementation(MOCKITO_CORE)
            implementation(TRUTH)
            implementation(KOTLIN_REFLECT)
            implementation MOCKITO_KOTLIN, {
                exclude group: 'org.mockito' // to keep control on the mockito version
            }
        }

        androidAndroidTest.dependencies {
            implementation project(":compose:ui:ui-test-font")
            implementation(ANDROIDX_TEST_RULES)
            implementation(ANDROIDX_TEST_RUNNER)
            implementation(ESPRESSO_CORE)
            implementation(JUNIT)
            implementation project(":ui:ui-test")
            implementation(DEXMAKER_MOCKITO, libs.exclude_bytebuddy) // DexMaker has it"s own MockMaker
            implementation(MOCKITO_CORE, libs.exclude_bytebuddy)
            implementation(TRUTH)
            implementation MOCKITO_KOTLIN, {
                exclude group: 'org.mockito' // to keep control on the mockito version
            }
        }
    }
}

androidx {
    name = "Compose UI Text"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenVersion = LibraryVersions.COMPOSE
    mavenGroup = LibraryGroups.Compose.UI
    inceptionYear = "2019"
    description = "Compose Text primitives and utilities"
    legacyDisableKotlinStrictApiMode = true
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        useIR = true
        freeCompilerArgs += ["-XXLanguage:+InlineClasses"]
    }
}

android.buildTypes.all {
    testCoverageEnabled = false
}
