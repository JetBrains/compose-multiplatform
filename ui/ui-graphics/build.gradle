/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.AndroidXUiPlugin
import androidx.build.LibraryGroups
import androidx.build.LibraryType
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static androidx.build.dependencies.DependenciesKt.*

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXUiPlugin")
}

AndroidXUiPlugin.applyAndConfigureKotlinPlugin(project)

if(!AndroidXUiPlugin.isMultiplatformEnabled(project)) {
    dependencies {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block below
         */

        api("androidx.annotation:annotation:1.2.0-beta01")
        api(project(":compose:ui:ui-unit"))

        implementation(project(":compose:runtime:runtime"))
        implementation(project(":compose:ui:ui-util"))
        implementation(KOTLIN_STDLIB_COMMON)

        testImplementation(ANDROIDX_TEST_RULES)
        testImplementation(ANDROIDX_TEST_RUNNER)
        testImplementation(JUNIT)
        testImplementation(TRUTH)

        androidTestImplementation(project(":compose:ui:ui-graphics:ui-graphics-samples"))
        androidTestImplementation(project(":compose:ui:ui-test-junit4"))
        androidTestImplementation(project(":compose:test-utils"))
        androidTestImplementation(ANDROIDX_TEST_RULES)
        androidTestImplementation(ANDROIDX_TEST_RUNNER)
        androidTestImplementation(ESPRESSO_CORE)
        androidTestImplementation(JUNIT)
        androidTestImplementation(TRUTH)

        lintPublish(project(":compose:ui:ui-graphics-lint"))
    }
}

if(AndroidXUiPlugin.isMultiplatformEnabled(project)) {
    kotlin {
        android()
        jvm("desktop")

        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block above
         */

        sourceSets {
            commonMain.dependencies {
                implementation(KOTLIN_STDLIB_COMMON)

                api(project(":compose:ui:ui-unit"))
                implementation(project(":compose:runtime:runtime"))
                implementation(project(":compose:ui:ui-util"))
            }

            androidMain.dependencies {
                api("androidx.annotation:annotation:1.2.0-beta01")
            }

            desktopMain.dependencies {
                implementation(KOTLIN_STDLIB)
                implementation(KOTLIN_STDLIB_JDK8)
                api(SKIKO)
            }

            androidTest.dependencies {
                implementation(ANDROIDX_TEST_RULES)
                implementation(ANDROIDX_TEST_RUNNER)
                implementation(JUNIT)
                implementation(TRUTH)
            }

            androidAndroidTest.dependencies {
                implementation(project(":compose:ui:ui-graphics:ui-graphics-samples"))
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(project(":compose:test-utils"))
                implementation(ANDROIDX_TEST_RULES)
                implementation(ANDROIDX_TEST_RUNNER)
                implementation(ESPRESSO_CORE)
                implementation(JUNIT)
            }

            desktopTest {
                resources.srcDirs += "src/desktopTest/res"
                dependencies {
                    implementation(project(":compose:ui:ui-test-junit4"))
                    implementation(JUNIT)
                    implementation(TRUTH)
                    implementation(SKIKO_CURRENT_OS)
                }
            }
        }
    }
}

androidx {
    name = "Compose Graphics"
    type = LibraryType.PUBLISHED_LIBRARY
    mavenGroup = LibraryGroups.Compose.UI
    inceptionYear = "2020"
    description = "Compose graphics"
    legacyDisableKotlinStrictApiMode = true
}

if(AndroidXUiPlugin.isMultiplatformEnabled(project)) {
    tasks.findByName("desktopTest").configure {
        systemProperties["GOLDEN_PATH"] = project.rootDir.absolutePath + "/../../golden"
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += [
            "-XXLanguage:+InlineClasses"
        ]
    }
}
