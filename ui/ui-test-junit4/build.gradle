/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.AndroidXComposePlugin
import androidx.build.LibraryGroups
import androidx.build.LibraryType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

AndroidXComposePlugin.applyAndConfigureKotlinPlugin(project)

android {
    lintOptions {
        disable("InvalidPackage")
    }
}

dependencies {
    kotlinPlugin(project(":compose:compiler:compiler"))

    if(!AndroidXComposePlugin.isMultiplatformEnabled(project)) {
        api(project(":compose:ui:ui-test"))
        api("androidx.activity:activity:1.2.0")
        api("androidx.test.ext:junit:1.1.2")
        api(libs.junit)
        api(libs.kotlinStdlib)
        api(libs.kotlinStdlibCommon)

        implementation(project(":compose:animation:animation-core"))
        implementation(project(":compose:runtime:runtime-saveable"))
        implementation(project(":activity:activity-compose"))
        implementation("androidx.annotation:annotation:1.1.0")
        implementation("androidx.lifecycle:lifecycle-common:2.3.0")
        implementation("androidx.lifecycle:lifecycle-runtime:2.3.0")
        implementation("androidx.test:core:1.3.0")
        implementation("androidx.test:monitor:1.3.0")
        implementation(libs.espressoCore)
        implementation(libs.espressoIdlingResource)
        implementation(libs.kotlinCoroutinesCore)
        implementation(libs.kotlinCoroutinesTest)

        testImplementation(project(":compose:material:material"))
        testImplementation(project(":compose:test-utils"))
        testImplementation(libs.truth)
        testImplementation(libs.robolectric)

        androidTestImplementation(project(":compose:test-utils"))
        androidTestImplementation(project(":compose:material:material"))
        androidTestImplementation(libs.testRules)
        androidTestImplementation(libs.testRunner)
        androidTestImplementation(libs.truth)
        androidTestImplementation(libs.mockitoCore)
        androidTestImplementation(libs.dexmakerMockito)
        androidTestImplementation(libs.mockitoKotlin)
    }
}


if(AndroidXComposePlugin.isMultiplatformEnabled(project)) {
    kotlin {
        android()
        jvm("desktop")

        sourceSets {
            commonMain.dependencies {
                implementation(project(":compose:ui:ui-text"))
                implementation(libs.kotlinStdlib)
            }

            jvmMain.dependencies {
                api(project(":compose:ui:ui-test"))
                api(libs.junit)
                api(libs.kotlinStdlib)
                api(libs.kotlinStdlibCommon)

                implementation("androidx.annotation:annotation:1.1.0")
                implementation(libs.kotlinCoroutinesCore)
                implementation(libs.kotlinCoroutinesTest)
            }

            androidMain.dependencies {
                api("androidx.activity:activity:1.2.0")
                implementation project(":activity:activity-compose")
                api("androidx.test.ext:junit:1.1.2")

                implementation(project(":compose:animation:animation-core"))
                implementation(project(":compose:runtime:runtime-saveable"))
                implementation("androidx.lifecycle:lifecycle-common:2.3.0")
                implementation("androidx.lifecycle:lifecycle-runtime:2.3.0")
                implementation("androidx.test:core:1.3.0")
                implementation("androidx.test:monitor:1.3.0")
                implementation(libs.espressoCore)
                implementation(libs.espressoIdlingResource)
            }

            test.dependencies {
                implementation(project(":compose:material:material"))
                implementation(project(":compose:test-utils"))
                implementation(libs.truth)
                implementation(libs.robolectric)
            }

            androidAndroidTest.dependencies {
                implementation(project(":compose:test-utils"))
                implementation(project(":compose:material:material"))
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.truth)
                implementation(libs.mockitoCore)
                implementation(libs.dexmakerMockito)
                implementation(libs.mockitoKotlin)
            }

            desktopMain.dependencies {
                implementation(libs.truth)
                implementation(libs.skiko)
            }

            androidMain.dependsOn(jvmMain)
            desktopMain.dependsOn(jvmMain)
            jvmMain.dependsOn(commonMain)
        }
    }
}

androidx {
    name = "Compose Testing for JUnit4"
    type = LibraryType.PUBLISHED_LIBRARY
    mavenGroup = LibraryGroups.Compose.UI
    inceptionYear = "2020"
    description = "Compose testing integration with JUnit4"
    legacyDisableKotlinStrictApiMode = true
}
