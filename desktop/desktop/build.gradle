/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.Publish
import androidx.build.RunApiTasks
import androidx.build.SupportConfigKt
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static androidx.build.AndroidXPlugin.BUILD_ON_SERVER_TASK
import static androidx.build.dependencies.DependenciesKt.*

plugins {
    id("AndroidXPlugin")
    id("AndroidXUiPlugin")
    id("kotlin-multiplatform")
}

dependencies {
    kotlinPlugin project(":compose:compiler:compiler")
}

kotlin {
    jvm() {
        withJava()
    }

    sourceSets {
        commonMain.dependencies {
            implementation(KOTLIN_STDLIB_COMMON)
            api project(":compose:foundation:foundation")
            api project(":compose:material:material")
            api project(":compose:runtime:runtime")
            api project(":compose:ui:ui")
        }

        jvmMain.dependencies {
            implementation(KOTLIN_STDLIB)
            implementation(KOTLIN_STDLIB_JDK8)
            implementation(KOTLIN_COROUTINES_CORE)
        }

        jvmTest {
            resources.srcDirs += new File(SupportConfigKt.getExternalProjectPath(project), "noto-fonts/other/")
            resources.srcDirs += "src/jvmTest/res"
            dependencies {
                implementation(KOTLIN_COROUTINES_TEST)
                implementation(SKIKO_CURRENT_OS)
                implementation project(":compose:ui:ui-test-junit4")
                implementation(JUNIT)
                implementation(TRUTH)
            }
        }
    }
}

tasks.findByName("jvmTest").configure {
    systemProperties['GOLDEN_PATH'] = project.rootDir.absolutePath + "/../../golden"
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        useIR = true
    }
}

androidx {
    name = "Jetpack Compose desktop implementation"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenGroup = LibraryGroups.Compose.DESKTOP
    inceptionYear = "2020"
    legacyDisableKotlinStrictApiMode = true
    runApiTasks = new RunApiTasks.No(
            "API tracking for desktop isn't supported at the moment, see b/163110180"
    )
}

PublicationContainer.metaClass.jvmOs = { name, skikoDeps ->
    delegate.create("jvm$name", MavenPublication) {
        artifactId = "${project.name}-jvm-$name"
        pom {
            withXml {
                def dependenciesNode = asNode().appendNode("dependencies")
                def desktopDependency = dependenciesNode.appendNode("dependency")
                desktopDependency.appendNode("groupId", group)
                desktopDependency.appendNode("artifactId", project.name)
                desktopDependency.appendNode("version", LibraryVersions.COMPOSE)
                desktopDependency.appendNode("scope", "compile")

                for (skiko in skikoDeps) {
                    def skikoParts = skiko.split(":")
                    def skikoDependency = dependenciesNode.appendNode("dependency")
                    skikoDependency.appendNode("groupId", skikoParts[0])
                    skikoDependency.appendNode("artifactId", skikoParts[1])
                    skikoDependency.appendNode("version", skikoParts[2])
                    skikoDependency.appendNode("scope", "runtime")
                }
            }
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            it.jvmOs("linux-x64", [SKIKO_LINUX_X64])
            it.jvmOs("macos-x64", [SKIKO_MACOS_X64])
            it.jvmOs("macos-arm64", [SKIKO_MACOS_ARM64])
            it.jvmOs("windows-x64", [SKIKO_WINDOWS_X64])
        }
    }
}

def projectPath = project.path
rootProject.tasks.named(BUILD_ON_SERVER_TASK).configure {
    dependsOn("$projectPath:jvmJar")
}