/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import androidx.build.AndroidXUiPlugin
import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.Publish
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static androidx.build.dependencies.DependenciesKt.*

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXUiPlugin")
}

AndroidXUiPlugin.applyAndConfigureKotlinPlugin(project)

dependencies {
    kotlinPlugin project(":compose:compiler:compiler")

    if(!AndroidXUiPlugin.isMultiplatformEnabled(project)) {
        /* When updating dependencies, make sure to make the an an analogous update in the
            corresponding block below */
        api project(":compose:runtime:runtime")
        api "androidx.annotation:annotation:1.1.0"

        implementation project(":compose:ui:ui-util")
        implementation(KOTLIN_STDLIB)

        testImplementation(JUNIT)
        testImplementation(TRUTH)
        testImplementation(ANDROIDX_TEST_CORE)
        testImplementation(ANDROIDX_TEST_RULES)

        androidTestImplementation project(':compose:ui:ui')
        androidTestImplementation project(":compose:ui:ui-test-junit4")
        androidTestImplementation "androidx.fragment:fragment:1.3.0-alpha05"
        androidTestImplementation(ANDROIDX_TEST_UIAUTOMATOR)
        androidTestImplementation(ANDROIDX_TEST_CORE)
        androidTestImplementation(ANDROIDX_TEST_RULES)
        androidTestImplementation(ANDROIDX_TEST_RUNNER)
        androidTestImplementation(ESPRESSO_CORE)
        androidTestImplementation(JUNIT)
        androidTestImplementation(TRUTH)
        androidTestImplementation(DEXMAKER_MOCKITO, libs.exclude_bytebuddy)
        // DexMaker has it"s own MockMaker
        androidTestImplementation(MOCKITO_CORE, libs.exclude_bytebuddy)
        // DexMaker has it"s own MockMaker
    }
}

if(AndroidXUiPlugin.isMultiplatformEnabled(project)) {
    kotlin {
        android()
        jvm("desktop")

        /* When updating dependencies, make sure to make the an an analogous update in the
            corresponding block above */
        sourceSets {
            commonMain.dependencies {
                implementation(KOTLIN_STDLIB_COMMON)

                api project(":compose:runtime:runtime")
                implementation project(":compose:ui:ui-util")

            }

            androidMain.dependencies {
                implementation(KOTLIN_STDLIB)
                api "androidx.annotation:annotation:1.1.0"
            }


            androidTest.dependencies {
                implementation(ANDROIDX_TEST_RULES)
                implementation(ANDROIDX_TEST_RUNNER)
                implementation(JUNIT)
                implementation(TRUTH)
            }

            androidAndroidTest.dependencies {
                implementation project(':compose:ui:ui')
                implementation project(":compose:ui:ui-test-junit4")
                implementation "androidx.fragment:fragment:1.3.0-alpha05"
                implementation(ANDROIDX_TEST_UIAUTOMATOR)
                implementation(ANDROIDX_TEST_CORE)
                implementation(ANDROIDX_TEST_RULES)
                implementation(ANDROIDX_TEST_RUNNER)
                implementation(ESPRESSO_CORE)
                implementation(JUNIT)
                implementation(TRUTH)
                implementation(DEXMAKER_MOCKITO, libs.exclude_bytebuddy)
                // DexMaker has it"s own MockMaker
                implementation(MOCKITO_CORE, libs.exclude_bytebuddy)
                // DexMaker has it"s own MockMaker
            }
        }
    }
}

androidx {
    name = "Compose Saved Instance State support"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenGroup = LibraryGroups.Compose.RUNTIME
    inceptionYear = "2020"
    description = "Compose components that allow saving and restoring the local ui state"
    legacyDisableKotlinStrictApiMode = true
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        useIR = true
    }
}
