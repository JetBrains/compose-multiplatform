/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.AndroidXUiPlugin
import androidx.build.LibraryGroups
import androidx.build.LibraryType
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static androidx.build.dependencies.DependenciesKt.*

plugins {
    id("AndroidXPlugin")
    id("AndroidXUiPlugin")
    id("com.android.library")
}

AndroidXUiPlugin.applyAndConfigureKotlinPlugin(project)

dependencies {
    kotlinPlugin(project(":compose:compiler:compiler"))

    if(!AndroidXUiPlugin.isMultiplatformEnabled(project)) {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block below
         */

        api(KOTLIN_COROUTINES_ANDROID)

        implementation("androidx.annotation:annotation:1.1.0")
        implementation(KOTLIN_STDLIB)

        testImplementation(KOTLIN_TEST_JUNIT)
        testImplementation(JUNIT)
        testImplementation(ROBOLECTRIC)
        testImplementation(KOTLIN_COROUTINES_TEST)

        androidTestImplementation(KOTLIN_TEST_JUNIT)
        androidTestImplementation(ANDROIDX_TEST_EXT_JUNIT)
        androidTestImplementation(ANDROIDX_TEST_RULES)
        androidTestImplementation(ANDROIDX_TEST_RUNNER)
        androidTestImplementation(JUNIT)
        androidTestImplementation(TRUTH)

        lintChecks(project(":compose:runtime:runtime-lint"))
        lintPublish(project(":compose:runtime:runtime-lint"))
    }
}

if(AndroidXUiPlugin.isMultiplatformEnabled(project)) {
    kotlin {
        android()
        jvm("desktop")

        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block above
         */
        sourceSets {
            commonMain.dependencies {
                implementation(KOTLIN_STDLIB_COMMON)
                implementation(KOTLIN_COROUTINES_CORE)
            }
            jvmMain.dependencies {
                implementation(KOTLIN_STDLIB)
                api(KOTLIN_COROUTINES_CORE)
            }
            androidMain {
                dependencies {
                    api(KOTLIN_COROUTINES_ANDROID)
                    api("androidx.annotation:annotation:1.1.0")
                }
                dependsOn(jvmMain)
            }
            desktopMain {
                dependsOn(jvmMain)
            }

            commonTest.dependencies {
                implementation(kotlin("test-junit"))
                implementation(KOTLIN_COROUTINES_TEST)
            }
            androidAndroidTest.dependencies {
                implementation(ANDROIDX_TEST_EXT_JUNIT)
                implementation(ANDROIDX_TEST_RULES)
                implementation(ANDROIDX_TEST_RUNNER)
                implementation(TRUTH)
            }
        }
    }
}

android {
    defaultConfig {
        consumerProguardFiles 'proguard-rules.pro'
    }
}

androidx {
    name = "Compose Runtime"
    type = LibraryType.PUBLISHED_LIBRARY
    mavenGroup = LibraryGroups.Compose.RUNTIME
    inceptionYear = "2019"
    description = "Tree composition support for code generated by the Compose compiler plugin and corresponding public API"
    legacyDisableKotlinStrictApiMode = true
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        incremental = false
    }
}
