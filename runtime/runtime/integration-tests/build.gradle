/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.AndroidXUiPlugin
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import static androidx.build.dependencies.DependenciesKt.*

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXUiPlugin")
}

AndroidXUiPlugin.applyAndConfigureKotlinPlugin(project)

dependencies {
    kotlinPlugin project(":compose:compiler:compiler")

    if(!AndroidXUiPlugin.isMultiplatformEnabled(project)) {
        androidTestImplementation(project(":compose:ui:ui"))
        androidTestImplementation project(":compose:ui:ui-test-junit4")
        androidTestImplementation(project(":compose:runtime:runtime"))

        androidTestImplementation(JUNIT)
        androidTestImplementation(KOTLIN_TEST_JUNIT)
        androidTestImplementation(ANDROIDX_TEST_EXT_JUNIT)
        androidTestImplementation(ANDROIDX_TEST_CORE)
        androidTestImplementation(ANDROIDX_TEST_RUNNER)
        androidTestImplementation(ANDROIDX_TEST_RULES)
        androidTestImplementation(KOTLIN_STDLIB)
        androidTestImplementation(KOTLIN_REFLECT)
        androidTestImplementation(TRUTH)
    }
}

if(AndroidXUiPlugin.isMultiplatformEnabled(project)) {
    kotlin {
        android()
        jvm("desktop")

        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block above
         */
        sourceSets {
            commonMain.dependencies {
                implementation(KOTLIN_STDLIB_COMMON)
                implementation(KOTLIN_COROUTINES_CORE)
                implementation(project(":compose:ui:ui"))
            }
            jvmMain.dependencies {
                implementation(KOTLIN_STDLIB)
                api(KOTLIN_COROUTINES_CORE)
            }
            androidMain {
                dependencies {
                    api(KOTLIN_COROUTINES_ANDROID)
                    api "androidx.annotation:annotation:1.1.0"

                    implementation("androidx.core:core-ktx:1.1.0")
                }
                dependsOn jvmMain
            }
            desktopMain {
                dependencies {
                    api(KOTLIN_COROUTINES_SWING)
                }
                dependsOn jvmMain
            }

            commonTest.dependencies {
                implementation kotlin("test-junit")
            }
            androidAndroidTest.dependencies {
                implementation(project(":compose:ui:ui"))
                implementation project(":compose:ui:ui-test-junit4")
                implementation(ANDROIDX_TEST_EXT_JUNIT)
                implementation(ANDROIDX_TEST_RULES)
                implementation(ANDROIDX_TEST_RUNNER)
                implementation(TRUTH)
            }
        }
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        incremental = false
    }
}
