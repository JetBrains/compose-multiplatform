/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.AndroidXComposePlugin
import androidx.build.LibraryType
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

AndroidXComposePlugin.applyAndConfigureKotlinPlugin(project)

dependencies {

    if(!AndroidXComposePlugin.isMultiplatformEnabled(project)) {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block below
         */

        api("androidx.annotation:annotation:1.1.0")
        api("androidx.compose.ui:ui:1.2.0-rc02")
        api("androidx.compose.ui:ui-unit:1.1.1")

        implementation("androidx.compose.runtime:runtime:1.2.0-rc02")
        implementation("androidx.compose.ui:ui-util:1.0.0")
        implementation("androidx.core:core:1.7.0")
        implementation("androidx.compose.animation:animation-core:1.1.1")
        implementation(libs.kotlinStdlibCommon)

        testImplementation(libs.testRules)
        testImplementation(libs.testRunner)
        testImplementation(libs.junit)
        testImplementation(libs.truth)

        androidTestImplementation(project(":compose:foundation:foundation"))
        androidTestImplementation("androidx.compose.ui:ui-test-junit4:1.0.0")
        androidTestImplementation(project(":compose:test-utils"))
        androidTestImplementation("androidx.activity:activity-compose:1.3.1")
        // old version of common-java8 conflicts with newer version, because both have
        // DefaultLifecycleEventObserver.
        // Outside of androidx this is resolved via constraint added to lifecycle-common,
        // but it doesn't work in androidx.
        // See aosp/1804059
        androidTestImplementation(project(":lifecycle:lifecycle-common-java8"))
        androidTestImplementation(libs.testRules)
        androidTestImplementation(libs.testRunner)
        androidTestImplementation(libs.junit)
        androidTestImplementation(libs.truth)

        samples(project(":compose:foundation:foundation-layout:foundation-layout-samples"))
    }
}

if(AndroidXComposePlugin.isMultiplatformEnabled(project)) {
    androidXComposeMultiplatform {
        android()
        desktop()
    }

    kotlin {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block above
         */
        sourceSets {
            commonMain.dependencies {
                implementation(libs.kotlinStdlibCommon)

                api(project(":compose:ui:ui"))
                implementation(project(":compose:runtime:runtime"))
                implementation(project(":compose:ui:ui-util"))
            }

            androidMain.dependencies {
                api("androidx.annotation:annotation:1.1.0")
                implementation("androidx.core:core:1.7.0")
                implementation("androidx.compose.animation:animation-core:1.1.1")
            }

            desktopMain.dependencies {
                implementation(libs.kotlinStdlib)
            }

            // TODO(b/214407011): These dependencies leak into instrumented tests as well. If you
            //  need to add Robolectric (which must be kept out of androidAndroidTest), use a top
            //  level dependencies block instead:
            //  `dependencies { testImplementation(libs.robolectric) }`
            androidTest.dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
            }

            androidAndroidTest.dependencies {
                implementation(project(":compose:foundation:foundation"))
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(project(":compose:test-utils"))
                implementation("androidx.activity:activity-compose:1.3.1")

                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
            }
        }
    }
    dependencies {
        samples(project(":compose:foundation:foundation-layout:foundation-layout-samples"))
    }
}

androidx {
    name = "Compose Layouts"
    type = LibraryType.PUBLISHED_LIBRARY
    mavenGroup = LibraryGroups.COMPOSE_FOUNDATION
    inceptionYear = "2019"
    description = "Compose layout implementations"
    legacyDisableKotlinStrictApiMode = true
}

android {
    namespace "androidx.compose.foundation.layout"
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += [
                "-Xjvm-default=all"
        ]
    }
}
