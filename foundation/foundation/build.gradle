/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import androidx.build.AndroidXUiPlugin
import androidx.build.LibraryGroups
import androidx.build.LibraryType
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static androidx.build.dependencies.DependenciesKt.*

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXUiPlugin")
}

AndroidXUiPlugin.applyAndConfigureKotlinPlugin(project)

dependencies {
    kotlinPlugin(project(":compose:compiler:compiler"))

    if(!AndroidXUiPlugin.isMultiplatformEnabled(project)) {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block above
         */
        api("androidx.annotation:annotation:1.1.0")
        api(project(':compose:animation:animation'))
        api(project(':compose:runtime:runtime'))
        api(project(':compose:ui:ui'))

        implementation(KOTLIN_STDLIB_COMMON)
        implementation(project(":compose:foundation:foundation-layout"))
        implementation(project(":compose:ui:ui-text"))
        implementation(project(":compose:ui:ui-util"))

        testImplementation(project(":compose:test-utils"))
        testImplementation(ANDROIDX_TEST_RULES)
        testImplementation(ANDROIDX_TEST_RUNNER)
        testImplementation(JUNIT)
        testImplementation(TRUTH)
        testImplementation(KOTLIN_COROUTINES_TEST)
        testImplementation(MOCKITO_CORE)
        testImplementation(KOTLIN_REFLECT)
        testImplementation(MOCKITO_KOTLIN, {
            exclude group: "org.mockito" // to keep control on the mockito version
        })

        androidTestImplementation(project(":compose:test-utils"))
        androidTestImplementation(project(":compose:ui:ui-test-font"))
        androidTestImplementation(project(":test-screenshot"))
        androidTestImplementation(ANDROIDX_TEST_UIAUTOMATOR)
        androidTestImplementation(ANDROIDX_TEST_RULES)
        androidTestImplementation(ANDROIDX_TEST_RUNNER)
        androidTestImplementation project(":activity:activity-compose")
        androidTestImplementation(ESPRESSO_CORE)
        androidTestImplementation(JUNIT)
        androidTestImplementation(TRUTH)
        androidTestImplementation(DEXMAKER_MOCKITO, excludes.bytebuddy) // DexMaker has it"s own
        // MockMaker
        androidTestImplementation(MOCKITO_CORE, excludes.bytebuddy) // DexMaker has it"s own MockMaker
        androidTestImplementation(MOCKITO_KOTLIN, {
            exclude group: "org.mockito" // to keep control on the mockito version
        })
    }
}

if(AndroidXUiPlugin.isMultiplatformEnabled(project)) {
    kotlin {
        android()
        jvm("desktop")

        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block above
         */
        sourceSets {
            commonMain.dependencies {
                implementation(KOTLIN_STDLIB_COMMON)
                api(project(':compose:animation:animation'))
                api(project(':compose:runtime:runtime'))
                api(project(':compose:ui:ui'))
                implementation(project(":compose:ui:ui-text"))
                implementation(project(":compose:ui:ui-util"))
                implementation(project(':compose:foundation:foundation-layout'))
            }
            androidMain.dependencies {
                api("androidx.annotation:annotation:1.1.0")
            }

            desktopMain.dependsOn(jvmMain)
            androidMain.dependsOn(jvmMain)

            desktopMain.dependencies {
                implementation(KOTLIN_STDLIB)
            }

            androidTest.dependencies {
                implementation(ANDROIDX_TEST_RULES)
                implementation(ANDROIDX_TEST_RUNNER)
                implementation(JUNIT)
                implementation(MOCKITO_CORE)
                implementation(TRUTH)
                implementation(KOTLIN_REFLECT)
                implementation(MOCKITO_KOTLIN, {
                    exclude group: "org.mockito" // to keep control on the mockito version
                })
            }

            commonTest.dependencies {
                implementation(KOTLIN_COROUTINES_TEST)
            }

            androidAndroidTest.dependencies {
                implementation(project(":compose:test-utils"))
                implementation(project(":compose:ui:ui-test-font"))
                implementation(project(":test-screenshot"))
                implementation(project(":activity:activity-compose"))

                implementation(ANDROIDX_TEST_UIAUTOMATOR)
                implementation(ANDROIDX_TEST_RULES)
                implementation(ANDROIDX_TEST_RUNNER)
                implementation(ESPRESSO_CORE)
                implementation(JUNIT)
                implementation(TRUTH)
                implementation(DEXMAKER_MOCKITO, excludes.bytebuddy) // DexMaker has it"s own
                // MockMaker
                implementation(MOCKITO_CORE, excludes.bytebuddy)
                // DexMaker has it"s own MockMaker
                implementation(MOCKITO_KOTLIN, {
                    exclude group: "org.mockito" // to keep control on the mockito version
                })
            }

            desktopTest.dependencies {
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(TRUTH)
                implementation(JUNIT)
                implementation(SKIKO_CURRENT_OS)
                implementation(MOCKITO_CORE)
                implementation(MOCKITO_KOTLIN, {
                    exclude group: "org.mockito" // to keep control on the mockito version
                })
            }
        }
    }
}

// Screenshot tests related setup
android {
    sourceSets.androidTest.assets.srcDirs +=
            project.rootDir.absolutePath + "/../../golden/compose/foundation/foundation"
}

androidx {
    name = "Compose Foundation"
    type = LibraryType.PUBLISHED_LIBRARY
    mavenGroup = LibraryGroups.Compose.FOUNDATION
    inceptionYear = "2018"
    description = "Higher level abstractions of the Compose UI primitives. This library is design system agnostic, providing the high-level building blocks for both application and design-system developers"
    legacyDisableKotlinStrictApiMode = true
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += ["-XXLanguage:+InlineClasses"]
    }
}
