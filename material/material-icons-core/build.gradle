/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import androidx.build.AndroidXComposePlugin
import androidx.build.LibraryType
import androidx.compose.material.icons.generator.tasks.IconGenerationTask

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

AndroidXComposePlugin.applyAndConfigureKotlinPlugin(project)

if (!AndroidXComposePlugin.isMultiplatformEnabled(project)) {
    dependencies {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block below
         */

        api("androidx.compose.ui:ui:1.0.0")
        implementation("androidx.compose.runtime:runtime:1.1.1")
        implementation(libs.kotlinStdlib)

        samples(project(":compose:material:material-icons-core:material-icons-core-samples"))
    }
}

if (AndroidXComposePlugin.isMultiplatformEnabled(project)) {

    androidXComposeMultiplatform {
        android()
        desktop()
    }

    kotlin {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block above
         */
        sourceSets {
            commonMain.dependencies {
                implementation(libs.kotlinStdlibCommon)
                api(project(":compose:ui:ui"))
            }

            androidMain.dependencies {
                implementation(libs.kotlinStdlib)
            }
        }
    }
    dependencies {
        samples(project(":compose:material:material-icons-core:material-icons-core-samples"))
    }
}

IconGenerationTask.registerCoreIconProject(
        project,
        android,
        AndroidXComposePlugin.isMultiplatformEnabled(project)
)

androidx {
    name = "Compose Material Icons Core"
    type = LibraryType.PUBLISHED_LIBRARY
    mavenGroup = LibraryGroups.COMPOSE_MATERIAL
    inceptionYear = "2020"
    description = "Compose Material Design core icons. This module contains the most commonly used set of Material icons."
    legacyDisableKotlinStrictApiMode = true
}

android {
    namespace "androidx.compose.material.icons"
}
